apply plugin: 'com.android.application'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'groovyx.android'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "build-time-tracker"

android {
    def specifiedVersionCode = project.hasProperty("versionCode") ? project.getProperty("versionCode") as Integer : 1
    def specifiedVersionName = project.hasProperty("versionName") ? project.getProperty("versionName") as String : "2.0-SNAPSHOT"

    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "com.one20.ota"
        minSdkVersion 21
        targetSdkVersion 25
        multiDexEnabled true
        versionCode specifiedVersionCode
        versionName specifiedVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    lintOptions {
        disable 'InvalidPackage'
    }
    dexOptions {
        preDexLibraries true
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            debuggable true
            testCoverageEnabled true
        }
        release {
            if (project.hasProperty("keystoreFile")) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    lintOptions {
        // lint won't respect the @TargetApi annotation ¯\_(ツ)_/¯
        disable 'NewApi'
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

buildtimetracker {
    reporters {
        csv {
            output "times.csv"
            append true
            header false
        }

        summary {
            ordered true
            threshold 50
            barstyle "unicode"
        }

        csvSummary {
            csv "times.csv"
        }
    }
}

kapt {
    generateStubs = true
}

tasks.whenTaskAdded { task -> if (task.name == "compileDebugGroovyWithGroovyc") { task.enabled = false } }
tasks.whenTaskAdded { task -> if (task.name == "compileQaGroovyWithGroovyc") { task.enabled = false } }
tasks.whenTaskAdded { task -> if (task.name == "compileReleaseGroovyWithGroovyc") { task.enabled = false } }

unMock {
    keep 'android.net.Uri'
    keep 'android.location.Location'
    keep 'android.text.TextUtils'
    keep 'android.util.Log'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:gridlayout-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    compile('com.mikepenz:materialdrawer:5.8.2@aar') {
        transitive = true
    }

    annotationProcessor 'com.squareup:javapoet:1.7.0'

    compile "com.google.dagger:dagger:2.11"
    annotationProcessor "com.google.dagger:dagger-compiler:2.11"
    kapt "com.google.dagger:dagger-compiler:2.11"
    provided 'javax.annotation:jsr250-api:1.0'

    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    kapt 'com.jakewharton:butterknife-compiler:8.5.1'

    compile 'frankiesardo:icepick:3.1.0'
    provided 'frankiesardo:icepick-processor:3.1.0'

    compile 'com.facebook.fresco:fresco:1.1.0'
    compile "com.facebook.fresco:imagepipeline-okhttp3:1.1.0"

    debugCompile 'com.facebook.stetho:stetho:1.4.2'
    debugCompile 'com.uphyca:stetho_realm:2.0.0'
    compile 'com.facebook.stetho:stetho-okhttp3:1.4.2'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    compile 'com.github.InkApplications:android-layout-injector:1.1.0'
    compile 'com.github.InkApplications:android-application-lifecycle:1.0.1'
    compile 'com.github.InkApplications:android-logger:4.1.0'
    compile 'com.github.InkApplications:guava-stopwatch:21.0'
    compile 'com.github.kylealanr:GestureDetectors:1.1'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.fernandocejas:arrow:1.0.0'

    compile 'io.reactivex.rxjava2:rxjava:2.0.7'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.jakewharton.rxrelay2:rxrelay:2.0.0'

    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.3@aar'
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'

    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.danikula:videocache:2.3.4'

    compile 'com.airbnb.android:epoxy:2.1.0'
    annotationProcessor 'com.airbnb.android:epoxy-processor:2.1.0'
    kapt 'com.airbnb.android:epoxy-processor:2.1.0'

    compile 'nl.littlerobots.rxlint:rxlint:1.2'

    compile 'net.danlew:android.joda:2.9.9'

    compile 'br.com.simplepass:loading-button-android:1.7.0'

    compile 'me.grantland:autofittextview:0.2.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.21'
    unmock 'org.robolectric:android-all:4.3_r2-robolectric-0'

    // Test Dependencies
    testCompile 'org.robospock:robospock:1.0.1'
    testCompile 'cglib:cglib:3.2.4'
    testCompile 'org.objenesis:objenesis:2.4'
    testCompile 'joda-time:joda-time:2.9.9'
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
    }
}

apply plugin: 'me.tatarka.retrolambda'
