apply plugin: 'com.android.application'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "build-time-tracker"

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "com.kyleriedemann.labombagigante"
        minSdkVersion 21
        targetSdkVersion 26
        multiDexEnabled true
        versionCode 1
        versionName "0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    dexOptions {
        preDexLibraries true
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            debuggable true
        }
        qa {
            signingConfig signingConfigs.debug
            minifyEnabled false
            applicationIdSuffix '.qa'
        }
        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
        }
    }
    testOptions {
        unitTests.all {
            jvmArgs '-noverify'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    lintOptions {
        disable 'InvalidPackage'
        // lint won't respect the @TargetApi annotation ¯\_(ツ)_/¯
        disable 'NewApi'
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

buildtimetracker {
    reporters {
        csv {
            output "times.csv"
            append true
            header false
        }

        summary {
            ordered true
            threshold 50
            barstyle "unicode"
        }

        csvSummary {
            csv "times.csv"
        }
    }
}

unMock {
    keep 'android.net.Uri'
    keep 'android.location.Location'
    keep 'android.text.TextUtils'
    keep 'android.util.Log'
    keep 'android.arch.lifecycle.ViewModel'
    keep 'android.content.Intent'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.android.support:gridlayout-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:support-core-utils:26.1.0'
    compile 'com.android.support:customtabs:26.1.0'

    compile "android.arch.lifecycle:runtime:1.0.3"
    compile "android.arch.lifecycle:extensions:1.0.0-rc1"
    annotationProcessor "android.arch.lifecycle:compiler:1.0.0-rc1"
    kapt "android.arch.lifecycle:compiler:1.0.0-rc1"

    compile('com.mikepenz:aboutlibraries:5.9.6@aar') {
        // this includes a colliding version of this guy's FastAdapter library if this
        // and MaterialDrawer are both marked as transitive
        transitive = false
    }

    compile('com.mikepenz:materialdrawer:5.8.2@aar') {
        transitive = true
    }

    // Koin
    compile "org.koin:koin-android:$koin_version"
    testCompile "org.koin:koin-test:$koin_version"

    compile 'com.facebook.fresco:fresco:1.1.0'
    compile "com.facebook.fresco:imagepipeline-okhttp3:1.1.0"

    debugCompile 'com.facebook.stetho:stetho:1.5.0'
    compile 'com.facebook.stetho:stetho-okhttp3:1.4.2'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    qaCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    compile 'com.github.InkApplications:android-logger:4.1.0'
    compile 'com.github.InkApplications:guava-stopwatch:21.0'
    compile 'com.github.kylealanr:GestureDetectors:1.1'
    compile 'com.github.u1f4f1:PowerBottomSheet:1.6'
    compile 'com.fernandocejas:arrow:1.0.0'

    compile 'io.reactivex.rxjava2:rxjava:2.1.3'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
    compile 'io.reactivex.rxjava2:rxkotlin:2.0.3'

    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'

    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    compile 'com.google.code.gson:gson:2.8.0'

    compile 'com.danikula:videocache:2.7.0'

    compile 'com.airbnb.android:epoxy:2.3.0'
    annotationProcessor 'com.airbnb.android:epoxy-processor:2.3.0'
    kapt 'com.airbnb.android:epoxy-processor:2.3.0'
    compile 'com.github.rubensousa:gravitysnaphelper:1.2'

    compile 'nl.littlerobots.rxlint:rxlint:1.2'

    compile 'net.danlew:android.joda:2.9.9'

    compile 'br.com.simplepass:loading-button-android:1.8.0'

    compile 'com.rengwuxian.materialedittext:library:2.1.4'

    compile 'com.github.moove-it:fakeit:v0.7'

    // Test Dependencies
    testCompile 'org.amshove.kluent:kluent:1.26'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.10.0'
    testCompile("com.nhaarman:mockito-kotlin:1.4.0") {
        exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib"
    }
    unmock 'org.robolectric:android-all:4.3_r2-robolectric-0'

    testCompile 'joda-time:joda-time:2.9.9'
}

configurations.all {
    resolutionStrategy {
        force 'org.jetbrains.kotlin:kotlin-stdlib:1.1.51'
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
    }
}
